###########################################################################
# Bisulphite sequencing (methylseq) analysis workflow
#
# This pipeline is adapted from current methyseq pipeline from BCB core
# This pipeline focuses on the second set of steps,
# Creator: Neelam Redekar, neelam.redekar@nih.gov
# Created: December 21, 2021
# Modifier: Tom Hill, tom.hill@nih.gov
# Modified: December 28, 2021
#
###########################################################################

from os.path import join
from snakemake.io import expand, glob_wildcards
from snakemake.utils import R
from os import listdir
import pandas as pd

##
## Locations of working directories and reference genomes for analysis
##
sample_file= config["samples"]
rawdata_dir= config["rawdata_dir"]
working_dir= config["result_dir"]
hg38_fa= config["hg38_fa"]
phage_fa= config["phage_fa"]
bisulphite_genome_path= config["bisulphite_ref"]
bisulphite_fa= config["bisulphite_fa"]
species= config["species"]

##
## Read in the masterkey file for 3 tab-delimited columns of samples, groups and comparison
## Each sample can be in the file multiple times if used in multiple comparisons, but will only be mapped/process once.
##
## e.g.
##
##sample	group	comp
##S1	GA	GAvsGB
##S2	GA	GAvsGB
##S3	GB	GAvsGB
##S4	GB	GAvsGB
##S5	GC	GAvsGC
##S6	GC	GAvsGC
##S1	GA	GAvsGC
##S2	GA	GAvsGC

## The file requires these headings as they are used in multiple rules later on.

## Here we read in the samples file generated and begin processing the data, printing out the samples and group comparisons

df = pd.read_csv(sample_file, header=0, sep='\t')

SAMPLES=list(set(df['sample'].tolist()))
GROUPS=list(set(df['comp'].tolist()))

print(SAMPLES)
print(len(SAMPLES))
print(GROUPS)
print(len(GROUPS))

CHRS = ['chr1','chr2','chr3','chr4','chr5','chr6','chr7','chr8','chr9','chr10','chr11','chr12','chr13','chr14','chr15','chr16','chr17','chr18','chr19','chr20','chr21','chr22','chr23','chrX']

RN = ['R1', 'R2']

rule All:
    input:
      # Creating data links:
      expand(join(working_dir, "raw/{samples}.{rn}.fastq.gz"), samples=SAMPLES, rn=RN),
      # Checking data quality:
      expand(join(working_dir, "rawQC/{samples}.{rn}_fastqc.html"), samples=SAMPLES, rn=RN),
      expand(join(working_dir, "trimQC/{samples}_val_1_fastqc.html"), samples=SAMPLES),
      expand(join(working_dir, "trimQC/{samples}_val_2_fastqc.html"), samples=SAMPLES),
      # Quality trimming output:
      expand(join(working_dir, "trimGalore/{samples}_val_1.fq.gz"),samples=SAMPLES),
      expand(join(working_dir, "trimGalore/{samples}_val_2.fq.gz"),samples=SAMPLES),
      # bwa-meth align to human reference genomes
      expand(join(working_dir, "bwaMethAlign/{samples}.bm_pe.flagstat"),samples=SAMPLES),
      expand(join(working_dir, "bwaMethAlign/{samples}.bm_pe.metrics.txt"),samples=SAMPLES),
      expand(join(working_dir, "bwaMethAlign/{samples}.bm_pe.deduplicated.flagstat"),samples=SAMPLES),
      # extract methylation sites
      expand(join(working_dir, "CpG/{samples}.bedGraph"),samples=SAMPLES),
      "multiqc_report.html"

## Copy raw data to working directory
rule raw_data_links:
    input:
      join(rawdata_dir, "{samples}.{rn}.fastq.gz")
    output:
      join(working_dir, "raw/{samples}.{rn}.fastq.gz")
    params:
      rname="raw_data_links",
      dir=directory(join(working_dir, "raw")),
    shell:
      """
      mkdir -p {params.dir}
      ln -s {input} {output}
      """

## Run fastqc on raw data to visually assess quality
rule raw_fastqc:
    input:
      join(working_dir, "raw/{samples}.{rn}.fastq.gz")
    output:
      join(working_dir, "rawQC/{samples}.{rn}_fastqc.html")
    params:
      rname="raw_fastqc",
      dir=directory(join(working_dir, "rawQC")),
      batch='--cpus-per-task=2 --mem=8g --time=8:00:00',
    threads:
      2
    shell:
      """
      module load fastqc/0.11.9
      mkdir -p {params.dir}
      fastqc -o {params.dir} -f fastq --threads {threads} --extract {input}
      """

## Trim raw data
rule trimmomatic:
    input:
      F1=join(working_dir, "raw/{samples}.R1.fastq.gz"),
      F2=join(working_dir, "raw/{samples}.R2.fastq.gz"),
    output:
      PE1=temp(join(working_dir, "trimmed_reads/{samples}.R1.pe.fastq.gz")),
      UPE1=temp(join(working_dir, "trimmed_reads/{samples}.R1.ue.fastq.gz")),
      PE2=temp(join(working_dir, "trimmed_reads/{samples}.R2.pe.fastq.gz")),
      UPE2=temp(join(working_dir, "trimmed_reads/{samples}.R2.ue.fastq.gz"))
    params:
      rname="trimmomatic",
      dir=directory(join(working_dir, "trimmed_reads")),
      batch='--cpus-per-task=8 --partition=norm --gres=lscratch:180 --mem=25g --time=20:00:00',
      command='ILLUMINACLIP:/usr/local/apps/trimmomatic/Trimmomatic-0.39/adapters/TruSeq3-PE-2.fa:2:30:10 LEADING:3 TRAILING:3 SLIDINGWINDOW:4:15 MINLEN:50'
    threads:
      8
    shell:
      """
      module load trimmomatic/0.39
      mkdir -p {params.dir}
      java -jar $TRIMMOJAR PE -phred33 -threads {threads} {input.F1} {input.F2} {output.PE1} {output.UPE1} {output.PE2} {output.UPE2} {params.command}
      """

## Second round of trimming/filtering
rule trimGalore:
    input:
      F1=join(working_dir, "trimmed_reads/{samples}.R1.pe.fastq.gz"),
      F2=join(working_dir, "trimmed_reads/{samples}.R2.pe.fastq.gz"),
    output:
      join(working_dir, "trimGalore/{samples}_val_1.fq.gz"),
      join(working_dir, "trimGalore/{samples}_val_2.fq.gz")
    params:
      rname="trimGalore",
      dir=directory(join(working_dir, "trimGalore")),
      tag='{samples}',
      fastqcdir=directory(join(working_dir, "postTrimQC")),
      command="--fastqc --clip_R1 10 --clip_R2 10 --three_prime_clip_R1 10 --three_prime_clip_R2 10 --length 50 --gzip",
      batch='--cpus-per-task=16 --partition=norm --gres=lscratch:100 --mem=25g --time=10:00:00',
    threads:
      16
    shell:
      """
      module load trimgalore/0.6.7
      module load fastqc/0.11.9
      mkdir -p {params.dir}
      trim_galore --paired --cores {threads} {params.command} --basename {params.tag} --output_dir {params.dir} --fastqc_args "--outdir {params.fastqcdir}"  {input.F1} {input.F2}
      """

## Run fastqc on filtered/trimmed data to visually assess quality for R1
rule trim_fastqc1:
    input:
      join(working_dir, "trimGalore/{samples}_val_1.fq.gz"),
    output:
      join(working_dir, "trimQC/{samples}_val_1_fastqc.html"),
    params:
      rname="trim_fastqc",
      dir=directory(join(working_dir, "trimQC")),
      batch='--cpus-per-task=2 --mem=8g --time=8:00:00',
    threads:
      2
    shell:
      """
      module load fastqc/0.11.9
      mkdir -p {params.dir}
      fastqc -o {params.dir} -f fastq --threads {threads} --extract {input}
      """

## Run fastqc on filtered/trimmed data to visually assess quality for R2
rule trim_fastqc2:
    input:
      join(working_dir, "trimGalore/{samples}_val_2.fq.gz"),
    output:
      join(working_dir, "trimQC/{samples}_val_2_fastqc.html"),
    params:
      rname="trim_fastqc",
      dir=directory(join(working_dir, "trimQC")),
      batch='--cpus-per-task=2 --mem=8g --time=8:00:00',
    threads:
      2
    shell:
      """
      module load fastqc/0.11.9
      mkdir -p {params.dir}
      fastqc -o {params.dir} -f fastq --threads {threads} --extract {input}
      """

rule bwa_meth:
  input:
    F1=join(working_dir, "trimGalore/{samples}_val_1.fq.gz"),
    F2=join(working_dir, "trimGalore/{samples}_val_2.fq.gz"),
  output:
    B1=temp(join(working_dir, "bwaMethAlign/{samples}.bm_pe.bam")),
    B2=join(working_dir, "bwaMethAlign/{samples}.bm_pe.flagstat"),
  params:
    rname="bwa_meth",
    dir=directory(join(working_dir,"bwaMethAlign")),
    genome=hg38_fa,
  threads:
    16
  shell:
    """
      module load bwa samtools/1.9 python
      source /data/$USER/conda/etc/profile.d/conda.sh
      conda activate meth
      mkdir -p {params.dir}
      module load samtools/1.9
      bwameth.py  --threads {threads} --reference {params.genome} {input.F1} {input.F2} | samtools view -@ {threads} -hb | samtools sort -@ {threads} -o {output.B1}
      samtools flagstat -@ {threads} {output.B1} > {output.B2}
    """

rule bwa_meth_phage:
  input:
    F1=join(working_dir, "trimGalore/{samples}_val_1.fq.gz"),
    F2=join(working_dir, "trimGalore/{samples}_val_2.fq.gz"),
  output:
    B1=join(working_dir, "bwaMethPhage/{samples}.bm_pe.bam"),
  params:
    rname="bwa_meth_phage",
    dir=directory(join(working_dir,"bwaMethPhage")),
    genome=phage_fa,
  threads:
    16
  shell:
    """
      module load bwa samtools python
      source /data/hillts/conda/etc/profile.d/conda.sh
      conda activate meth
      module load samtools/1.9
      mkdir -p {params.dir}
      bwameth.py  --threads {threads} --reference {params.genome} {input.F1} {input.F2} | samtools view  -F 4 -@ {threads} -hb | samtools sort -@ {threads} -o {output.B1}
    """

rule bwa_meth_dedup:
    input:
      B1=join(working_dir, "bwaMethAlign/{samples}.bm_pe.bam"),
    output:
      B1=temp(join(working_dir, "bwaMethAlign/{samples}.bm_pe.deduplicated.bam")),
      M1=temp(join(working_dir, "bwaMethAlign/{samples}.bm_pe.metrics.txt")),
      B2=join(working_dir, "bwaMethAlign/{samples}.bm_pe.deduplicated.flagstat"),
    params:
      rname="bwa_meth_dedup",
      dir=directory(join(working_dir, "bwaMethAlign")),
    threads:
      16
    shell:
      """
      module load picard
      mkdir -p {params.dir}
      java -Xmx20g -XX:ParallelGCThreads={threads} -jar $PICARDJARPATH/picard.jar MarkDuplicatesWithMateCigar -I {input.B1} -O {output.B1} -M {output.M1} --MINIMUM_DISTANCE 200
      samtools flagstat -@ {threads} {output.B1} > {output.B2}
      """

rule extract_CpG_bwa_meth:
    input:
      F1=join(working_dir, "bwaMethAlign/{samples}.bm_pe.deduplicated.bam"),
    output:
      B1=join(working_dir, "CpG/{samples}.bedGraph"),
    params:
      rname="extract_CpG_bwa_meth",
      dir=directory(join(working_dir, "CpG_bwa")),
      genome=hg38_fa,
      prefix=join(working_dir,"CpG/{samples}"),
    threads:
      16
    shell:
      """
      module load python
      module load samtools
      mkdir -p {params.dir}
      source /data/hillts/conda/etc/profile.d/conda.sh
      conda activate meth
      module load samtools/1.9
      MethylDackel extract -o {params.prefix} -@ {threads} {params.genome} {input.F1}
      """

rule multiqc:
  input:
    expand(join(working_dir, "bwaMethAlign/{samples}.bm_pe.bam"),samples=SAMPLES),
    expand(join(working_dir, "bwaMethAlign/{samples}.bm_pe.deduplicated.bam"),samples=SAMPLES),
  output:
    "multiqc_report.html",
  params:
    dir=working_dir,
    bis_dir=directory(join(working_dir,"bwaMethAlign")),
    script_dir=join(working_dir,"scripts"),
  shell:
    """
    module load multiqc/1.9 bismark
    cd {params.bis_dir}
    bismark2report
    bismark2summary
    cd {params.dir}
    multiqc --ignore '*/.singularity/*' -f --interactive .
    """
